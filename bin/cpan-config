#!/usr/bin/env perl
use strict;
BEGIN { $^W = 1 }

my %myconfig;
BEGIN {
  local $CPAN::Config;
  my $f = "$ENV{HOME}/.cpan/CPAN/MyConfig.pm";
  if (-e $f) {
    do $f;
    if (defined $CPAN::Config) {
      %myconfig = %$CPAN::Config;
    }
  }
  delete $INC{$f};
}

use CPAN ();
use ExtUtils::MakeMaker ();
use File::Basename qw(dirname);
use Cwd;

my $helper_root = $ENV{HELPERS_ROOT} ||= Cwd::abs_path(dirname(__FILE__).'/..');

{
  package FakeFrontEnd;
  sub AUTOLOAD { 1 }
}

my %config = (
  %myconfig,
  urllist => [ $ENV{CPAN_MIRROR} || 'http://www.cpan.org/' ],
  prerequisites_policy  => 'follow',
  prefs_dir             => "$helper_root/share/distroprefs",
  patches_dir           => "$helper_root/share/patches",
  recommends_policy     => 0,
);

{
  $CPAN::Frontend = 'FakeFrontEnd';
  BEGIN { eval { require warnings; 'warnings'->unimport('redefine') } }
  local *ExtUtils::MakeMaker::prompt = sub ($;$) {
    $_[0] =~ /manual configuration/ ? 'no' : $_[1];
  };
  open STDOUT, '>/dev/null';
  open STDERR, '>/dev/null';
  $CPAN::Config = { %config };
  eval { CPAN::Config->load };
  $CPAN::Config = { %config };
  CPAN::Config->init;
  CPAN::Config->commit;
}
